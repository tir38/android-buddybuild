apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'org.stoyicker.android-check'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.buddybuild"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 4
        versionName "0.3"
        testInstrumentationRunner 'com.buddybuild.IntegrationTestRunner' // for instrumented integration tests
    }
    signingConfigs {
        release {
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-test-rules.pro'
        }
        release {
            minifyEnabled false // TODO why is this false?
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-test-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = true
        javaMaxHeapSize "2g"
    }
    lintOptions {
        abortOnError true
        ignoreWarnings false
        warningsAsErrors true
        lintConfig file('../config/lint.xml')
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

check {
    checkstyle {
        config '../config/checkstyle.xml'
        abortOnError true
    }
    findbugs { skip true }
    pmd { skip true }
}

dependencies {
    // TODO finish transition to new AGP
    // https://developer.android.com/studio/preview/features/new-android-plugin-migration.html#new_configurations
    // once we don't have a million proguard-related errors

    implementation project(':core')
    implementation project(':rest')

    // Better logging
    // https://github.com/JakeWharton/timber
    // Apache 2.0
    implementation 'com.jakewharton.timber:timber:' + rootProject.ext.timberLibVersion

    // Dependency injection
    // http://google.github.io/dagger/
    // Apache 2.0
    implementation "com.google.dagger:dagger:" + rootProject.ext.daggerLibVersion
    annotationProcessor "com.google.dagger:dagger-compiler:" + rootProject.ext.daggerLibVersion

    // RxJava2 and Reactive Extensions for Android
    // https://github.com/ReactiveX/RxJava
    // https://github.com/ReactiveX/RxAndroid
    // Apache 2.0
    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Dependency Injection for Views
    // http://jakewharton.github.io/butterknife/
    // Apache 2.0
    implementation 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    // Easily add item decoration to recyclerviews
    // http://bignerdranch.github.io/simple-item-decoration/
    // MIT
    implementation 'com.bignerdranch.android:simple-item-decoration:1.0.0'

    // Expand recycler view elements
    // https://bignerdranch.github.io/expandable-recycler-view/
    // MIT
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'

    // An adaptation of the JSR-310 backport for Android
    // (i.e. better date and time classes)
    // https://github.com/JakeWharton/ThreeTenABP
    // Apache 2.0
    implementation 'com.jakewharton.threetenabp:threetenabp:' + rootProject.ext.threetenapbLibVersion

    // Automatically terminate RxStreams at certain lifecycle events
    // https://github.com/trello/RxLifecycle
    // Apache 2.0
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android-lifecycle:2.1.0'

    // support libs; not all of these are directly used by the app
    // some are used by third-party libs and we need to ensure they are all on the same version number
    // so override third-party libs dependencies' by declaring them as top level dependencies
    implementation "com.android.support:appcompat-v7:" + rootProject.ext.supportLibVersion
    implementation "com.android.support:recyclerview-v7:" + rootProject.ext.supportLibVersion
    implementation "com.android.support:design:" + rootProject.ext.supportLibVersion

    // Lifecycles, LiveData, and Viewmodel Architecture Components
    implementation ("android.arch.lifecycle:runtime:1.0.3") {
        exclude group: 'com.android.support'
    }
    implementation ("android.arch.lifecycle:extensions:1.0.0") {
        exclude group: 'com.android.support'
    }
    annotationProcessor ("android.arch.lifecycle:compiler:1.0.0") {
        exclude group: 'com.android.support'
    }

    // Buddybuild SDK
    // https://docs.buddybuild.com/index.html
    // closed source
    implementation 'com.buddybuild:sdk:0.0.84@aar'

    // Android device shake detection
    // used with Buddy Build
    // https://github.com/square/seismic
    // Apache 2.0
    implementation 'com.squareup:seismic:1.0.2'

    // Application Crash Reports for Android
    // used with Buddy Build
    // https://github.com/ACRA/acra
    // Apache 2.0
    implementation 'com.faendir:acra:4.10.0'

    // ---------------
    // LOCAL UNIT TEST DEPENDENCIES
    // ---------------
    testCompile('org.threeten:threetenbp:1.3.2') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }

    testImplementation 'junit:junit:' + rootProject.ext.junitVersion

    testImplementation "org.assertj:assertj-core:$rootProject.ext.assertJVersion"

    testImplementation "org.mockito:mockito-core:2.13.0"

    testImplementation "org.robolectric:robolectric:3.4.2"
    testImplementation "org.robolectric:shadows-support-v4:3.4-rc2"

    // LEFT OFF HERE: we have to have these dependencies "in-app" for test to work. Does this cause Dagger to stop
    // working in-app?
    implementation 'com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.1'
    implementation 'com.github.stephanenicolas.toothpick:smoothie:1.1.1'
    annotationProcessor 'com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.1'
    testImplementation 'com.github.stephanenicolas.toothpick:toothpick-testing:1.1.1'
    testAnnotationProcessor 'com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.1'

    // ---------------
    // INSTRUMENTATION TEST DEPENDENCIES
    // ---------------
    androidTestImplementation 'junit:junit:' + rootProject.ext.junitVersion
    androidTestImplementation "org.assertj:assertj-core:$rootProject.ext.assertJVersion"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.ext.rulesVersion"

    // Easy mock http requests
    // https://github.com/andrzejchm/RESTMock
    // Apache 2.0
    androidTestImplementation "com.github.andrzejchm.RESTMock:android:$rootProject.ext.restMockVersion"
}
